plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
    id "org.javacc.javacc" version "3.0.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation 'net.portswigger.burp.extensions:montoya-api:2023.10.4'
    implementation 'org.swinglabs:swingx:1.6.1'
    implementation 'com.github.CoreyD97:Burp-Montoya-Utilities:54678c64'
    implementation 'co.elastic.clients:elasticsearch-java:8.8.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'

    testRuntimeOnly files("${System.properties['user.home']}/BurpSuiteCommunity/burpsuite_community.jar")
}

sourceSets {
    main {
        java {
            srcDir compileJavacc.outputDirectory
            srcDir compileJjtree.outputDirectory
        }
    }
}

compileJjtree {
    include '**/*.java'
}

jar {
    baseName = project.name
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        (configurations.runtimeClasspath).collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/releases")
}
